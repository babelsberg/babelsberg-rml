module javascript:
  with "../objects/babelsberg.rml"
  with "assertions.rml"

  relation printprogram: babelsberg.Program => ()
  relation printassert: babelsberg.Program => ()
end

relation printprogram: babelsberg.Program => () =
  rule	printPreamble() &
	printS(statement, "    ") &
	printPostscript()
	-----------------
	printprogram(babelsberg.PROGRAM(statement))
end

relation printassert: babelsberg.Program => () =
  rule	print "    assert(" &
	printE(babelsberg.LVALUE(l)) & print " === " & printE(e) &
	print ");\n"
	----------------------------------------
	printassert(babelsberg.PROGRAM(babelsberg.ASSIGN(l,babelsberg.DEREF(e))))

  rule	print "    assert(fieldEquals(" &
	printE(babelsberg.LVALUE(l)) & print ", " & printE(babelsberg.IRECORD(fieldexps)) &
	print "));\n"
	----------------------------------------
	printassert(babelsberg.PROGRAM(babelsberg.ASSIGN(l,babelsberg.IRECORD(fieldexps))))

  rule	print "    assert(" &
	printE(babelsberg.LVALUE(l)) & print " == " & printE(e) &
	print ");\n"
	----------------------------------------
	printassert(babelsberg.PROGRAM(babelsberg.ASSIGN(l,e)))
end

relation printPreamble: () => () =
  rule	print "test" &
	Assertions.exampleId() => s & print s &
	print ": function() {\n" &
	print "    bbb.defaultSolver = new CommandLineZ3();\n" &
	print "    var ctx = {unsat: false};\n\n"
	--------------------------------------------------
	printPreamble()
end

relation printPostscript: () => () =
  rule	print "},\n"
	--------------------------------------------------
	printPostscript()
end

relation printS: (babelsberg.Statement, string) => () =
  axiom printS(babelsberg.SKIP, i) => ()

  rule  printS(s1, i) & printS(s2, i)
	-----------------------
	printS(babelsberg.SEQ(s1,s2), i)

  rule	string_append(i, "    ") => i' &
	print i & print "if (" & printE(e) & print ") {\n" &
	printS(s1, i') &
	print i & print "} else {\n" &
	printS(s2, i') &
	print i & print "}\n"
	----------------------------------------------
	printS(babelsberg.IF(e, s1, s2), i)

  rule	string_append(i, "    ") => i' &
	print i & print "while (" & printE(e) & print ") {\n" &
	printS(s, i') &
	print i & print "}\n"
	-----------------------
	printS(babelsberg.WHILE(e, s), i)

  rule	print i & print "try {\n" &
	print i & print "    " & printE(babelsberg.LVALUE(l)) & print " = " & printE(e) & print ";\n" &
	print i & print "} catch (e) { ctx.unsat = true }\n" &
	Assertions.assert(2)
	---------------------------------------------------------
	printS(babelsberg.ASSIGN(l,e), i)

  rule  print i & print "try { once: {\n" &
	print i & print "    " & printE(babelsberg.IDENTITY(e0, e1)) & print "\n" &
	print i & print "} } catch (e) { ctx.unsat = true }\n"
	------------------------------------------------------------
	printS(babelsberg.ONCE(babelsberg.CONSTRAINT(babelsberg.REQUIRED, babelsberg.IDENTITY(e0, e1))), i)

  rule  print i & print "try { always: {\n" &
	print i & print "    " & printE(babelsberg.IDENTITY(e0, e1)) & print "\n" &
	print i & print "} } catch (e) { ctx.unsat = true }\n"
	--------------------------------------------------------------
	printS(babelsberg.ALWAYS(babelsberg.CONSTRAINT(babelsberg.REQUIRED, babelsberg.IDENTITY(e0, e1))), i)

  rule	print i & print "try { once: {\n" &
	print i & print "    " & print "priority: '" & printRho(rho) & print "';\n" &
	print i & print "    " & printE(e) & print "\n" &
	print i & print "} } catch (e) { ctx.unsat = true }\n" &
	Assertions.assert(1)
	-------------------------
	printS(babelsberg.ONCE(babelsberg.CONSTRAINT(rho,e)), i)

  rule	print i & print "try { always: {\n" &
	print i & print "    " & print "priority: '" & printRho(rho) & print "';\n" &
	print i & print "    " & printE(e) & print "\n" &
	print i & print "} } catch (e) { ctx.unsat = true }\n" &
	Assertions.assert(1)
	-------------------------
	printS(babelsberg.ALWAYS(babelsberg.CONSTRAINT(rho,e)), i)
end

relation printE: babelsberg.Exp => () =
  rule	print "true"
	------------
	printE(babelsberg.VALUE(babelsberg.K(babelsberg.TRUE)))

  rule	print "false"
	------------
	printE(babelsberg.VALUE(babelsberg.K(babelsberg.FALSE)))

  rule	print "null"
	------------
	printE(babelsberg.VALUE(babelsberg.K(babelsberg.NIL)))

  rule	print s
	-------------------------------
	printE(babelsberg.VALUE(babelsberg.K(babelsberg.STRING(s))))

  rule	real_string(r) => s &
	print s
	-------
	printE(babelsberg.VALUE(babelsberg.K(babelsberg.REAL(r))))

  rule	string_length(x) => i &
	i - 1 => li &
	string_nth(x, li) => lst &
	lst = #"?" &
	string_list(x) => chars &
	list_delete(chars, li) => chars' &
	list_string(chars') => xs &
	print "ro(ctx." & print xs & print ")"
	-------------------------------------
	printE(babelsberg.LVALUE(babelsberg.VARIABLE(x)))

  rule	print "ctx." & print x
	----------------------
	printE(babelsberg.LVALUE(babelsberg.VARIABLE(x)))

  rule	print "(" & printE(e) & print ")" &
	print "." & print l
	-------------------
	printE(babelsberg.LVALUE(babelsberg.FIELD(e, l)))

  rule	printE(e1) & printOp(op) & printE(e2)
	-------------------------------------
	printE(babelsberg.OP(e1, op, e2))

  rule	printE(e1) & printComp(op) & printE(e2)
	-------------------------------------
	printE(babelsberg.COMPARE(e1, op, e2))

  rule	printE(e1) & printComb(op) & printE(e2)
	-------------------------------------
	printE(babelsberg.COMBINE(e1, op, e2))

  rule	printE(e1) & print " === " & printE(e2)
	---------------------------------------
	printE(babelsberg.IDENTITY(e1, e2))

  rule	e = babelsberg.VALUE(babelsberg.K(babelsberg.NIL)) & print l &
	print "(" & printEachArg(argexps) & print ")"
	-------------------------------------------
	printE(babelsberg.CALL(e,l,argexps))

  rule	printE(e) & print "." & print l &
	print "(" & printEachArg(argexps) & print ")"
	-------------------------------------------
	printE(babelsberg.CALL(e,l,argexps))

  rule	print "new Object({" & printFields(fieldexps) & print "})"
	---------------------------------------------------------
	printE(babelsberg.UIDRECORD(fieldexps))

  rule	print "{" & printFields(fieldexps) & print "}"
	---------------------------------------------------------------
	printE(babelsberg.IRECORD(fieldexps))
end

relation printEachArg: babelsberg.Exp list => () =
  axiom	printEachArg([]) => ()

  rule	printE(e)
	---------
	printEachArg(e :: [])

  rule	printE(e) & print ", " &
	printEachArg(rest)
	------------------
	printEachArg(e :: rest)
end

relation printFields: babelsberg.ObjectLiteral => () =
  axiom	printFields([]) => ()

  rule	print l & print ": " & printE(e)
	--------------------------------
	printFields((l,e) :: [])

  rule  print l & print ": " & printE(e) &
	print ", " & printFields(rest)
	------------------------------
	printFields((l,e) :: rest)
end

relation printOp: babelsberg.Op => () =
  rule	print " + "
	-----------
	printOp(babelsberg.ADD)

  rule	print " - "
	-----------
	printOp(babelsberg.SUB)

  rule	print " / "
	-----------
	printOp(babelsberg.DIV)

  rule	print " * "
	-----------
	printOp(babelsberg.MUL)
end

relation printComp: babelsberg.Comp => () =
  rule	print " < "
	-----------
	printComp(babelsberg.LESSTHAN)

  rule	print " <= "
	-----------
	printComp(babelsberg.LEQUAL)

  rule	print " == "
	-----------
	printComp(babelsberg.EQUAL)

  rule	print " != "
	-----------
	printComp(babelsberg.NEQUAL)

  rule	print " >= "
	-----------
	printComp(babelsberg.GEQUAL)

  rule	print " > "
	-----------
	printComp(babelsberg.GREATERTHAN)
end

relation printComb: babelsberg.Comb => () =
  rule	print " && "
	-----------
	printComb(babelsberg.AND)

  rule	print " || "
	-----------
	printComb(babelsberg.OR)
end

relation printRho: babelsberg.Rho => () =
  rule	print "weak"
	--------------
	printRho(babelsberg.WEAK)

  rule	print "medium"
	--------------
	printRho(babelsberg.MEDIUM)

  rule	print "required"
	--------------
	printRho(babelsberg.REQUIRED)
end
